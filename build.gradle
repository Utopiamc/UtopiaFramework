group 'de.utopiamc'
version '1.0-RELEASE'

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    apply plugin: "maven-publish"

    apply from: "$rootProject.projectDir/dependencies.gradle"
    apply from: "$rootProject.projectDir/secrets.gradle"

    group 'de.utopiamc'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            url = "https://repo.cloudnetservice.eu/repository/releases/"
        }
    }

    dependencies {
        annotationProcessor libs.lombok

        testImplementation testLibs.jupiterApi
        testImplementation testLibs.jupiterEngine
    }

    configurations {
        compileOnly.extendsFrom annotationProcessor
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    test {
        useJUnitPlatform()
    }

    compileJava {
        options.compilerArgs << '-parameters'
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/utopiamc/utopiaframework")
                credentials {
                    username = secrets.githubUsername
                    password = secrets.githubToken
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }

}